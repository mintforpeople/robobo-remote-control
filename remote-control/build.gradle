apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 22
        versionName "1.0.5-SNAPSHOT"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.6.1'

    implementation 'com.mytechia:robobo-framework:1.3.4'
    implementation 'com.google.code.gson:gson:2.8.9'


    testImplementation 'junit:junit:4.13.2'

}


/*
New local publish module
TODO: Make it able to release on OSS Sonatype to MavenCentral. Not needed by now, we can just upload to the local repo.

Using maven-publish and standard methods to push to maven.
To do the same on other modules, just replace the Chris Bane's script with this
Use gradle publishing:publishLibraryPublicationToLocalNexusRepository to upload artifacts to the repo

If the version ends in -SNAPSHOT, the snapshot repo will be selected.
All of the release info is stored in gradle.properties

Nexus auth should be done in the user's home gradle.properties file (uncommited for security reasons)
 */

afterEvaluate {
    publishing {
        repositories {
            maven {
                name 'LocalNexus'
                allowInsecureProtocol = true
                credentials {
                    username NEXUS_USER
                    password NEXUS_PASSWORD
                }
                if(VERSION_NAME.endsWith('-SNAPSHOT')) {
                    url SNAPSHOT_REPOSITORY_URL
                } else {
                    url RELEASE_REPOSITORY_URL
                }
            }
        }

        publications {
            library(MavenPublication) {
                from components.release
                groupId GROUP
                artifactId POM_ARTIFACT_ID
                version VERSION_NAME
            }
        }
    }
}

